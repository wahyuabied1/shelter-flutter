allprojects {
    repositories {
        google()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.10'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10'
        }
    }
}

rootProject.buildDir = '../build'

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            if (project.android.namespace == null) {
                project.android.namespace = project.group ?: "com.${project.name}"
            }

            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }

            // Force Kotlin JVM target untuk semua plugin
            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
