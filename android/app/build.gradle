plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id 'com.google.firebase.firebase-perf'
    id "com.google.firebase.crashlytics"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.shelter.indonesia.superapps"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    
    configurations.all {
        resolutionStrategy {
            force 'androidx.annotation:annotation:1.8.0'
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
        }
    }

    defaultConfig {
        applicationId "com.shelter.indonesia.superapps"
        minSdk 23
        targetSdk 35
        compileSdk 35
        ndkVersion flutter.ndkVersion

        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders = [
                googleMapsKey: "",
        ]
        multiDexEnabled true
    }

    signingConfigs {
        debug {

        }

        release {

        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }
        release {
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir "../../build/app/intermediates/merged_native_libs"
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug  // Use debug signing temporarily
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            resValue "string", "app_name", "ShelterSuperApp DEV"
            applicationIdSuffix ".debug"
            manifestPlaceholders += []
        }

        prod {
            dimension "app"
            resValue "string", "app_name", "ShelterSuperApp"
            manifestPlaceholders += []
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation(platform('com.google.firebase:firebase-bom:32.8.1'))
    // Add the dependencies for the Crashlytics NDK and Analytics libraries
    implementation("com.google.firebase:firebase-crashlytics-ndk")
    implementation("com.google.firebase:firebase-analytics")

    //MANDATORY for App Inbox
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.google.android.gms:play-services-appset:16.0.2'
    
    // Force Kotlin version to avoid D8 errors
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
    
    // Force compatible annotation version
    implementation 'androidx.annotation:annotation:1.8.0'

}

task printVersionName{
    doLast {
        println android.defaultConfig.versionName
    }
}
