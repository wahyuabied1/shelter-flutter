plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id 'com.google.firebase.firebase-perf'
    id "com.google.firebase.crashlytics"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.shelter.indonesia.superapps"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.shelter.indonesia.superapps"
        minSdk 23
        targetSdk 34
        compileSdk 34
        ndkVersion flutter.ndkVersion
        ndk {
            /**
             * What are the supported target architectures?
             *
             * When building your application in release mode, Flutter apps can be compiled for
             * armeabi-v7a (ARM 32-bit), arm64-v8a (ARM 64-bit), and x86-64 (x86 64-bit).
             * Flutter supports building for x86 Android through ARM emulation.
             */
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }

        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders = [
                googleMapsKey: "",
        ]
        multiDexEnabled true
    }

    signingConfigs {
        debug {

        }

        release {

        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir "../../build/app/intermediates/merged_native_libs"
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            resValue "string", "app_name", "ShelterSuperApp DEV"
            applicationIdSuffix ".debug"
            manifestPlaceholders += []
        }

        prod {
            dimension "app"
            resValue "string", "app_name", "ShelterSuperApp"
            manifestPlaceholders += []
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation(platform('com.google.firebase:firebase-bom:33.1.2'))
    // Add the dependencies for the Crashlytics NDK and Analytics libraries
    implementation("com.google.firebase:firebase-crashlytics-ndk")
    implementation("com.google.firebase:firebase-analytics")

    //MANDATORY for App Inbox
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.google.android.gms:play-services-appset:16.0.2'

}

task printVersionName{
    doLast {
        println android.defaultConfig.versionName
    }
}
